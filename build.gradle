buildscript {
    buildscript { scriptHandler ->
        apply from: 'buildscript.gradle', to: scriptHandler
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.+"
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
    }
}

apply plugin: 'idea'
apply plugin: 'eclipse'

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

subprojects {
    group = 'me.insidezhou.instep'
    version = '1.0.0-SNAPSHOT'

    ext.kotlinVersion = '1.2.51'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'org.jetbrains.dokka'

    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'

    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/milestone" }
    }

    dependencies {
        testImplementation group: 'org.testng', name: 'testng', version: '6.9.13'
    }

    jar {
        baseName "$rootProject.name-$project.name"
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        options.encoding = "UTF-8"
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    dokka {
        outputFormat = "javadoc"
        outputDirectory = "$buildDir/javadoc"
    }

    task docJar(type: Jar, dependsOn: dokka) {
        classifier = 'javadoc'
        from dokka.outputDirectory
    }

    task publishToSonatype {
        group "publishing"
        description "发布到Sonatype仓库"
        dependsOn tasks.withType(PublishToMavenRepository).matching {
            it.repository == publishing.repositories.sonatype
        }
    }

    test {
        useTestNG()
    }

    publishing {
        publications {
            all(MavenPublication) {
                artifactId jar.baseName

                from components.java

                artifact sourceJar
                artifact docJar

                pom {
                    name = 'Instep'
                    description = 'Tools are designed to make coding easier.'
                    url = 'https://github.com/InsideZhou/Instep'

                    scm {
                        connection = 'scm:git:git://github.com/InsideZhou/Instep.git'
                        developerConnection = 'scm:git:ssh://github.com/InsideZhou/Instep.git'
                        url = 'https://github.com/InsideZhou/Instep'

                    }

                    licenses {
                        license {
                            name = 'The 2-Clause BSD License'
                            url = 'https://opensource.org/licenses/BSD-2-Clause'
                        }
                    }

                    developers {
                        developer {
                            name = 'Inside Zhou'
                            email = 'inside@insidezhou.me'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                def isSnapshot = version.endsWith("SNAPSHOT")
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

                url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
                name 'sonatype'
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.all
    }
}
