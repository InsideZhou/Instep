buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.5.0"
    }
}

subprojects {
    group = 'me.insidezhou.instep'
    version = '2.1.0'

    apply plugin: 'java'
    apply plugin: 'java-library'

    apply plugin: 'kotlin'
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: 'org.jetbrains.dokka'

    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }
    }

    dependencies {
        testImplementation group: 'org.testng', name: 'testng', version: '6.9.13'
    }

    jar {
        baseName "$rootProject.name-$project.name"
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        options.encoding = "UTF-8"
    }

    task sourceJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task docJar(type: Jar, dependsOn: dokkaJavadoc) {
        archiveClassifier.set('javadoc')
        from dokkaJavadoc.outputDirectory
    }

    test {
        useTestNG()
    }

    publishing {
        publications {
            full(MavenPublication) {
                artifactId jar.archiveBaseName.get()

                from components.java

                artifact sourceJar
                artifact docJar

                pom {
                    name = 'Instep'
                    description = 'Tools are designed for more easier coding.'
                    url = 'https://github.com/InsideZhou/Instep'

                    scm {
                        connection = 'scm:git:git://github.com/InsideZhou/Instep.git'
                        developerConnection = 'scm:git:ssh://github.com/InsideZhou/Instep.git'
                        url = 'https://github.com/InsideZhou/Instep'

                    }

                    licenses {
                        license {
                            name = 'The 2-Clause BSD License'
                            url = 'https://opensource.org/licenses/BSD-2-Clause'
                        }
                    }

                    developers {
                        developer {
                            name = 'Inside Zhou'
                            email = 'inside@insidezhou.me'
                        }
                    }
                }
            }

            simple(MavenPublication) {
                artifactId jar.archiveBaseName.get()

                from components.java

                artifact sourceJar
                artifact docJar
            }
        }

        repositories {
            maven {
                def isSnapshot = version.endsWith("SNAPSHOT")
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

                url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
                name 'sonatype'
                credentials {
                    username = project.findProperty("ossrhUsername")
                    password = project.findProperty("ossrhPassword")
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.full
    }
}
