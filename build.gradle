buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.+"
    }
}

subprojects {
    group = 'me.insidezhou.instep'
    version = '0.1.0-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'

    configurations {
        provided
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
    }

    dependencies {
        testCompile group: 'org.testng', name: 'testng', version: '6.9.+'
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        options.encoding = "UTF-8"
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    dokka {
        outputFormat = "javadoc"
        outputDirectory = "$buildDir/javadoc"
    }

    task docJar(type: Jar, dependsOn: dokka) {
        classifier = 'javadoc'
        from dokka.outputDirectory
    }

    test {
        useTestNG()
    }

    publishing {
        publications {
            source(MavenPublication) {
                artifact sourceJar
            }

            doc(MavenPublication) {
                artifact docJar
            }
        }
    }
}
